= Vector extension
Proposal for "low-config" redesign 
:doctype: article
:encoding: utf-8
:lang: en
:toc: left
:numbered:
:stem: latexmath

== Implementation-defined constant parameters

These parameters are fixed for a given machine.  The ISA supports
writing code that is portable across machines with different values
for these parameters, under certain constraints.

The maximum size of a single vector element in bits, stem:[ELEN \geq
max(XLEN,FLEN) ]. Must be a power of 2.

The number of bits in a vector register, stem:[VLEN \geq ELEN].  Must
be a power of 2.

NOTE: Platform profiles may set further constraints on these
parameters, for example, requiring a minimm stem:[VLEN] or stem:[ELEN] value.

NOTE: Vector contexts cannot be migrated across vector units with
different VLEN and ELEN settings.

== Vector Register File

There are 32 architectural vector registers, `v0`-`v31`.

Each vector register has a fixed stem:[VLEN] bits of state.

If the system has floating-point registers, the floating-point
register ``f``__x__ is contained in the low stem:[FLEN] bits of vector
register ``v``__x__.

[source]
----
Example, FLEN=32, VLEN=64

bytes        7 6 5 4 3 2 1 0
            |       v0      |
                    |  f0   |
----

NOTE: Because bits within an element are numbered in a little-endian
format with increasing bit index corresponding to increasing
magnitude, and because RISC-V is little-endian, vector register
layouts are drawn with bytes ordered from right to left with
increasing byte address.
 
NOTE: Zfinx ("F in X") is a new ISA option under consideration where
floating-point instructions take their arguments from the integer
register file.  The vector extension is incompatible with this option.
Overlaying vectors on the integer registers for Zfinx would need
different code to avoid integer registers with special meanings in the
ABI, e.g., `x0` and `x1`.

==  Vector CSRs

Three new XLEN-width unprivileged CSRs are added: `vtype`, `vl`,
`vlmax`.

== Vector type register, `vtype`

The XLEN-wide _vector_ _type_ CSR, `vtype` provides the default type
of variables contained in the vector register file, and enables a
polymorphic interpretation of existing instructions. The vector type
also determines the number of elements that are held in each vector
register.

The `type` register has three fields, `vshp`, `vrep`, and `vsew`.

[source]
----
vtype layout

XLEN-1:6    Reserved (write 0)
     5:3    vsew[2:0]
     2:1    vrep[1:0]
       0    vshp
----

Not all combinations of values in the three fields will be supported
on an implementation.

NOTE: Further standard and custom extensions to the vector base will
extend these three fields to support a greater variety of data types.

=== Vector shape `vshp` encoding

[source]
----
 'vshp' shape field encoding:

 0 scalar
 1 vector
----

When the vector unit is set to scalar shape, all operations only
operate on the first element (index 0) of the vector registers and the
vector length setting `vl` is ignored.

In vector shape, most instructions operate on vectors of elements.

=== Vector representation `vrep` encoding

The `vrep` field specifies how the bit patterns stored in each element
are to be interpeted.  Integer instructions can also explicitly treat
their operands as signed or unsigned.

[source]
----
 'vrep[1:0]' representation field encoding

 00  IEEE-754/2008 floating-point
 01  reserved         
 10  Unsigned binary integer
 11  Signed two's-complement integer  
----

=== Vector standard element width `vsew`

The value in `vsew` sets the dynamic _standard_ _element_ _width_
(SEW).  By default, a vector register is viewed as being divided into
stem:[VLMAX = \frac{VLEN}{SEW}] standard elements (always an integer
power of 2).  The VLMAX derived from SEW is used to control the number
of iterations of standard stripmining loops.

[source]
----
  vsew[2:0] (standard element width) encoding

  vsew  SEW
  ---  ----
  000  FLEN (or XLEN if no F)
  001     8
  010    16
  011    32
  100    64
  101   128
  110   256
  111   512
----

In systems with floating-point extensions, a value of 0 in `vsew` sets
SEW=FLEN.  In systems without floating-point extensions, a value of 0
in `vsew` sets SEW=XLEN.

NOTE: For example, a machine with stem:[VLEN=128] has the following
stem:[VLMAX] values for the following stem:[SEW] values:
(stem:[SEW=32b, VLMAX=4]); (stem:[SEW=16b, VLMAX=8]); (stem:[SEW=8b, VLMAX=16]).

=== Interaction of `vtype` and  standard floating-point code

A setting of `vtype`=0 corresponds to a scalar shape, with IEEE FP
representation, and FLEN bits per element in systems with
floating-point extensions.  With this setting, standard floating-point
instructions behave as if there were no vector extension present.

When the vector extension is added, the conventional calling
convention is augmented to require that `vtype` is 0 at entry and exit
from any function.  This requirement ensures that the behavior of
existing floating-point code that is unaware of the vector extension
is unchanged.

== Vector Length register `vl`

The stem:[XLEN]-bit-wide read-only `vl` CSR can only be updated by the
`vsetvli` and `vsetvl` instructions.

The `vl` register holds an unsigned integer specifying the number of
elements to be updated by a vector instruction.  Elements in the
destination vector with indices stem:[\geq vl] are not updated during
execution of a vector instruction.  As a degenerate case, when `vl`=0,
no elements are updated in the destination vector.

== Maximum vector Length register `vlmax`

The XLEN-wide `vlmax` CSR is a read-only register whose value is
derived from the other state in the system.  The `vlmax` register
holds an unsigned integer representing the largest number of elements
that can be completed by a single vector instruction with the current
`vtype` setting.  The value in `vlmax`stem:[= \frac{VLEN}{SEW}].

== `vsetvli`/`vsetvl` instructions

----
 vsetvli rd, rs1, vtypei # rd = new vl, rs1 = AVL, vtypei = new vtype setting
 vsetvl  rd, rs1, rs2    # rd = new vl, rs1 = AVL, rs2 = new vtype value
----

The `vsetvli` instruction sets the `vtype`, `vl`, and `vlmax` CSRs
based on its arguments, and writes the new value of `vl` into `rd`.

The new `vtype` setting is encoded in the immediate field `vtypei` for
`vsetvli` and in the `rs2` register for `vsetvl`.

NOTE: The immediate argument `vtypei` can be a compressed form of the
full vtype setting, capturing the most common use cases.

The `vtype` setting must be supported by the implementation, and the
 `vsetvl{i}` instruction will raise an illegal instruction exception
 if the setting is not supported.

NOTE: Would simplify pipelines if `vtype` value errors were flagged at
use not write, but somehow need to catch errant code without requiring
full XLEN bits in `vtype` when only a few bits are actually needed.
Can specify WARL but that would hide errors.

The requested application vector length (AVL) is passed in `rs1` as an
unsigned integer.

The `vlmax` register is set to stem:[VLMAX] based on the new
stem:[SEW] in the `vtype` setting .

=== Constraints on setting `vl`

The resulting `vl` setting must satisfy the following constraints:

. `vl = AVL` if `AVL \<= VLMAX`
. `vl >= ceil(AVL / 2)` if `AVL < (2 * VLMAX)`
. `vl = VLMAX` if `AVL >= (2 * VLMAX)`
. Deterministic on any given implementation for same input AVL and `vtype` values
. These specific properties follow from the prior rules:
.. `vl = 0` if  `AVL = 0`
.. `vl > 0` if `AVL > 0`
.. `vl \<= VLMAX`
.. `vl \<= AVL`


[NOTE]
--
The `vl` setting rules are designed to be sufficiently strict to
preserve `vl` behavior across register spills and context swaps for
`AVL \<= VLMAX`, yet flexible enough to enable implementations to improve
vector lane utilization for `AVL > VLMAX`.

For example, this permits an implementation to set `vl = ceil(AVL / 2)`
for `VLMAX < AVL < 2*VLMAX` in order to evenly distribute work over the
last two iterations of a stripmine loop.
Requirement 2 ensures that the first stripmine iteration of reduction
loops uses the largest vector length of all iterations, even in the case
of `AVL < 2*VLMAX`.
This allows software to avoid needing to explicitly calculate a running
maximum of vector lengths.
--

=== `vsetvl` instruction

The `vsetvl` variant operates similary to `vsetvli` except that it
takes a `vtype` value from `rs2` and can be used for context restore,
and when `vtypei` can not hold desired setting.

NOTE: Several active complex types can be held in different `x`
registers and swapped in as needed using `vsetvl`.

== Vector element mapping to vector register state

To represent a variety of different width datatypes in the same
fixed-width vector registers, the mapping used between vector elements
and bytes in a vector register depends on the implementation's fixed
ELEN and the runtime SEW setting.

NOTE: Previous RISC-V vector proposals hid this mapping from software,
whereas this proposal has a specific mapping for all configurations,
which reduces implementation flexibilty but removes need for zeroing
on config changes.  Making the mapping explicit also has the advantage
of simplifying oblivious context save-restore code, as the code can
save the configuration in `vl`, `vlmax`, and `vtype`, then reset
`vtype` to a convenient value (e.g., vectors of ELEN) before saving
all vector register bits without needing to parse the configuration.
The reverse process will restore the state.

NOTE: This packing is designed to minimize datapath wiring complexity
when dealing with different bitwidth datatypes.

NOTE: Although implementations with different ELEN have different byte
packings, vector machine code can be written to produce the same
result on different implementations.

The following diagrams illustrate how different width elements are
packed into the bytes of a vector register depending on ELEN and the
current SEW setting.  The element index is shown placed at the
least-significant byte of the stored element.

[source]
----
 ELEN=32b

 Byte         3 2 1 0

 SEW=8b       3 2 1 0
 SEW=16b        1   0
 SEW=32b            0

 ELEN=64b

 Byte        7 6 5 4 3 2 1 0

 SEW=8b      7 6 5 4 3 2 1 0
 SEW=16b       3   2   1   0
 SEW=32b           1       0
 SEW=64b                   0


 ELEN=128b

 Byte        F E D C B A 9 8 7 6 5 4 3 2 1 0

 SEW=8b      F E D C B A 9 8 7 6 5 4 3 2 1 0
 SEW=16b       7   6   5   4   3   2   1   0
 SEW=32b           3       2       1       0
 SEW=64b                   1               0
 SEW=128b                                  0
----

When stem:[ VLEN > ELEN], the element numbering continues into the
following stem:[ELEN]-wide units.

----
 ELEN unit        3       2       1       0
 Byte          3 2 1 0 3 2 1 0 3 2 1 0 3 2 1 0

 SEW=8b        F E D C B A 9 8 7 6 5 4 3 2 1 0
 SEW=16b         7   6   5   4   3   2   1   0
 SEW=32b             3       2       1       0
----

Some vector instructions have some operands that are wider than the
current SEW setting.  In this case, a group of vector registers are
used to provide storage for the wider operands as shown below.

When an instruction has an operand twice as wide as SEW, e.g., a
vector load of 32-bit words when SEW=16b, then an even-odd pair of
vector registers are used to hold the double-width value as shown
below:

----
 Example 1: ELEN=32
 ELEN unit      3       2       1       0    
 Byte        3 2 1 0 3 2 1 0 3 2 1 0 3 2 1 0 
 SEW=16b       7   6   5   4   3   2   1   0   <=16-bit elements
 v2*n              6       4       2       0   32-bit elements
 v2*n+1            7       5       3       1 
----

The even-numbered vector register holds the even-numbered elements of
the double-width vector, while the odd-numbered vector register holds
the odd-numbered elements of the double-width vector.

NOTE: The pattern of storing elements in the pair of vector registers
is designed to simplify datapath alignment for mixed-width operations.

For quad-width operands that are stem:[4\times SEW] a group of four
aligned vector registers are used to hold the results:

----
 ELEN unit        3       2       1       0    
 Byte          3 2 1 0 3 2 1 0 3 2 1 0 3 2 1 0 

 SEW=8b        F E D C B A 9 8 7 6 5 4 3 2 1 0   8b elements
 v4*n                C       8       4       0   32b elements
 v4*n+1              D       9       5       1 
 v4*n+2              E       A       6       2 
 v4*n+3              F       B       7       3 
----

NOTE: A similar pattern is followed for octa-width operands
stem:[8\times SEW], though not clear that is necessary in mandatory
base.

=== Supporting Mixed-Width Operations at Full Throughput

Additional `setvli` variants are provided to modify SEW to handle
double-width elements in a loop.

[source]
----
setvl2ci rs1, vtypei  # sets vtypei, then sets vl according to AVL=ceil(rs1/2)
setvl2fi rs1, vtypei  # sets vtypei, then sets vl according to AVL=floor(rs1/2)

Example: Load 16-bit values, widen multiply to 32b, shift 32b result
right by 3, store 32b values.

loop:
    setvli t0, a0, VT_VINT16 # vtype = 16-bit integer vectors
    vlh v2, (a1)              # Get 16b vector
      slli t0, t0, 1
      add a1, a1, t0          # Bump pointer
    vmulw.vs v0, v2, v3       # 32b in <v0,v1> pair
    setvl2ci a0, VT_VINT32    # Ceil half length in 32b (can fuse with following)
    vsrl.vi v0, v0, 3        # Elements 0, 2, 4,...
    vsetvl2fi a0, VT_VINT32   # Floor half length in 32b (can fuse with following)
    vsrl.vi v1, v1, 3        # Elements 1, 3, 5,...
    vsetvli t0, a0, VT_VINT16 # Back to 16b
    vsw v0, (a2)              # Store vector of 32b <v0,v1> pair
      sub a0, a0, t0          # Decrement count
      slli t0, t0, 2
      add a2, a2, t0          # Bump pointer
      bnez a0, loop           # Any more?

Alternative loop only using wider elements:

loop:
    setvli t0, a0, VT_INT32 # Use only 32-bit elements
    vlh v0, (a1)            # Sign-extend 16b load values to 32b elements 
      sll t1, t0, 1
      add a1, a1, t1        # Bump pointer
    vmul.vs  v0, v0, v3     # 32b multiply result
    vsrl.vi  v0, v0, 3      # Shift elements
    vsw v0, (a2)            # Store vector of 32b results
      sll t1, t0, 2         
      add a2, a2, t1        # Bump pointer
      sub a0, a0, t0
      bnez a0, loop         # Any more?

The first loop is more complex but may have greater performance on
machines where 16b widening multiplies are faster than 32b integer
multiplies.  Also, the 16b vector load may run faster due to the
larger number of elements per iteration.
----

This technique allows for multiple wider operations to be performed
natively on each half of the wider vector.  Conversion operations
allow values to be copied into the double-width format, or back into
the single-width formate.

Other forms for quad (and octal) widths:

[source]
----
setvl4ci    #set correct length for vector v4*n
setvl4di    #set correct length for vector v4*n+1
setvl4ei    #set correct length for vector v4*n+2
setvl4fi    #set correct length for vector v4*n+3
----


== Vector instruction formats

Vector loads and stores move bit patterns between vector register
elements and memory.

Vector arithmetic instructions operate on values held in vector
register elements.

Vector instructions can have scalar or vector source operands and
produce scalar or vector results.  Scalar operands and results are
located in element 0 of a vector register.

== Vector masking

Masking is supported on almost all vector instructions producing
vectors, with the mask supplied by vector register `v0`.  The `LSB` of
each stem:[SEW]-wide element in `v0` is used as the mask, in either
true or complement form.  Element operations that are masked off do
not modify the destination vector register element and never generate
exceptions.  Instructions producing scalars are not maskable.

The type of masking is encoded in a two-bit `m[1:0]` field
(`inst[26:25]`) for all vector instructions.

[source]
----
m[1:0]

  00    vector, where v0[0] = 0
  01    vector, where v0[0] = 1
  10    reserved
  11    always true
----

== Vector Load/Store Encoding

The vector extension uses the floating-point load and store encoding
space.  The exisitng scalar floating-point loads and stores are used
for scalar loads and stores, and also for vector loads and stores.

=== Operation of Floating-Point Load/Store Instructions in Vector Extension

The standard FDQ floating-point extensions' loads and stores retain
their original meaning when `vtype` is set to scalar shape.

The standard floating-point loads (FLH, FLW, FLD, FLQ), read a single
value from memory and update the low stem:[FLEN] bits of the
destination vector register.  Floating-point types narrower than
stem:[FLEN] are NaN-boxed, setting upper bits to 1.  If stem:[VLEN >
FLEN], the upper bits of the vector register are unchanged by the
floating-point load.

The standard floating-point stores (FSH, FSW, FSD, FDQ) read the
appropriate number of bits from the least-significant bits of the
vector register and write these to memory.

When `vtype` is set to a vector shape, standard floating-point loads
do not NaN-box narrower values out to FLEN bits, instead they leave
the upper bits unchanged in the vector register.  So, when FLEN=64, an
FLW only updates the bottom 32 bits of the vector register.

The standard floating-point load and store instructions do not
interpret the bits that they move and so are unaffected by changes to
the `vrep` field in `vtype`.

An implementation must provide all the standard floating-point loads
and stores corresponding to bitwidths up to stem:[ELEN].  For example,
in a system with stem:[FLEN=32] and stem:[ELEN=128], all of the H, W,
D, Q loads and stores must be provided.

In vector mode, the new FLB/FSB instructions are added to move a
single byte to/from the low 8 bits of a vector register.

=== Vector Load/Store Instructions

These are encoded within the floating-point load and store encoding space.

[source]
----
 Width   vshp=scalar  vshp=vector

        Mem    Reg     Mem   Reg
        Bits   Bits    Bits  Bits
 000      8    FLEN  vl*8     vl*8
 001     16    FLEN  vl*16    vl*16
 010     32    FLEN  vl*32    vl*32
 011     64    FLEN  vl*64    vl*64
 100      -    FLEN  vl*SEW   vl*SEW   
 101      -    FLEN  vl*2*SEW vl*2*SEW 
 110      -      -   vl*4*SEW vl*4*SEW 
 111      -      -   vl*8*SEW vl*8*SEW 
----

When `vshp`=vector, a portion of the 12-bit immediate field is
repurposed to provide further vector instruction encoding, with
bits[26:25] holding the mask information.

[source]
----
  31 30 29 28 27 26 25 24 23 22 21 20  Loads
  31 30 29 28 27 26 25 11 10  9  8  7  Stores
       simm5     m1 m0       funct5       

funct5 encodes:
       unit-stride
       unit-stride speculative
       constant-stride
       indexed
----

Vector load/store instructions move bit patterns between vector
register elements and memory.  Vector load instructions can optionally
sign- or zero-extend narrower memory values into wider vector register
element destinations based on current `vrep` setting.

NOTE: Could also provide explicit sign-extension bits or override
using funct5 bits.

Vector unit-stride, constant-stride, and indexed (scatter/gather)
load/store instructions are supported.

NOTE: Vector AMO instructions are not provided in the base vector
extension.

NOTE: Separate floating-point loads are not provided.  A consequence
is that narrower floating-point values are not automatically NaN-boxed
in a wider destination element.  A separate instruction could be
provided to 1-extend a narrower type to provide explicit NaN boxing if
necessary, but this is not proposed for base.

==== Vector load instructions

==== unit-stride instructions
[source,asm]
----
    # vd destination, rs1 base address, vm is mask encoding
    vlb     vd, rs1, vm
    vlh     vd, rs1, vm
    vlw     vd, rs1, vm
    vld     vd, rs1, vm
    vle     vd, rs1, vm  # SEW
    vle2     vd, rs1, vm # 2*SEW
    vle4     vd, rs1, vm # 4*SEW
    vle8     vd, rs1, vm # 8*SEW
----

[NOTE]
Speculative versions for unit-stride loads only in base?

==== constant-stride instructions
[source,asm]
----
    # vd destination, rs1 base address, rs2 byte stride
    vlsb    vd, offset(rs1), rs2, vm
    vlsh    vd, offset(rs1), rs2, vm
    vlsw    vd, offset(rs1), rs2, vm
    vlsd    vd, offset(rs1), rs2, vm
    vlse    vd, offset(rs1), rs2, vm  # SEW  
    vlse2   vd, offset(rs1), rs2, vm  # 2*SEW
    vlse4   vd, offset(rs1), rs2, vm  # 4*SEW
    vlse8   vd, offset(rs1), rs2, vm  # 8*SEW
----

The offset is encoded as an immediate (size TBD) that is then scaled
by the element size to give a byte offset.

The stride is interpreted as an integer representing a byte offset.

==== indexed (scatter-gather) instructions
[source,asm]
----
    # vd destination, rs1 base address, vs2 indices
    vlxb    vd, offset(rs1), vs2, vm
    vlxh    vd, offset(rs1), vs2, vm
    vlxw    vd, offset(rs1), vs2, vm
    vlxd    vd, offset(rs1), vs2, vm
    vlxe    vd, offset(rs1), vs2, vm  # SEW  
    vlxe2   vd, offset(rs1), vs2, vm  # 2*SEW
    vlxe4   vd, offset(rs1), vs2, vm  # 4*SEW
    vlxe8   vd, offset(rs1), vs2, vm  # 8*SEW
----

The offset is encoded as an immediate (size TBD) that is then scaled
by the element size to give a byte offset.

Scatter/gather indices are treated as signed integers and are
sign-extended from the currently stem:[SEW] width to `XLEN`
representing byte offsets.

NOTE: Might ideally want to support index vectors wider than
stem:[SEW], but adds some complexity and probably no space in base.

=== Vector stores

Vector stores move data values as bits taken from the LSBs of the
source element.  If the store datatype is wider than stem:[SEW], then
multiple vector registers are used to supply the data as described
above.

==== unit-stride store instructions
[source,asm]
----
    vsb     vs3, rs1, vm
    vsh     vs3, rs1, vm
    vsw     vs3, rs1, vm
    vsd     vs3, rs1, vm
    vse     vs3, rs1, vm  # SEW  
    vse2    vs3, rs1, vm  # 2*SEW
    vse4    vs3, rs1, vm  # 4*SEW
    vse8    vs3, rs1, vm  # 8*SEW
----

==== constant-stride store instructions
[source,asm]
----
    vssb    vs3, offset(rs1), rs2, vm
    vssh    vs3, offset(rs1), rs2, vm
    vssw    vs3, offset(rs1), rs2, vm
    vssd    vs3, offset(rs1), rs2, vm
    vsse    vs3, offset(rs1), rs2, vm  # SEW  
    vsse2   vs3, offset(rs1), rs2, vm  # 2*SEW
    vsse4   vs3, offset(rs1), rs2, vm  # 4*SEW
    vsse8   vs3, offset(rs1), rs2, vm  # 8*SEW
----

==== indexed store (scatter) instructions
[source,asm]
----
    vsxb    vs3, offset(rs1), vs2, vm
    vsxh    vs3, offset(rs1), vs2, vm
    vsxw    vs3, offset(rs1), vs2, vm
    vsxd    vs3, offset(rs1), vs2, vm
----

==== indexed-unordered (scatter-gather) instructions (not in base spec)
[source,asm]
----
    vsxub   vs3, offset(rs1), vs2, vm
    vsxuh   vs3, offset(rs1), vs2, vm
    vsxuw   vs3, offset(rs1), vs2, vm
    vsxud   vs3, offset(rs1), vs2, vm
----

==== indexed-reverse-ordered (scatter-gather) instructions (not in base spec)

[source,asm]
----
    vsxrb   vs3, offset(rs1), vs2, vm
    vsxrh   vs3, offset(rs1), vs2, vm
    vsxrw   vs3, offset(rs1), vs2, vm
    vsxrd   vs3, offset(rs1), vs2, vm
----



=== Vector Arithmetic Instruction encoding

The vector arithmetic instructions reuse the standard scalar
floating-point opcodes.  When `vshp`=vector, the rounding mode is
always set to dynamic, and the 3-bit `rm` field is repurposed to
in encoding.


[source]
----
Encoding of operand pattern rm field

rm2 rm1 rm0
0     0   0      Vector-vector
0     0   1      Vector-vector
0     1   0      Vector-vector
0     1   1      Vector-vector
1     0   0      Vector-scalar
1     0   1      Vector-scalar
1     1   0      Vector-scalar
1     1   1      Vector-vector
----


=== Vector-Vector and Vector-Scalar Arithmetic Instructions

Most vector arithmetic instructions have both vector-vector (`.vv`),
where both operands are vectors of elements, and vector-scalar
(`.vs`), where the second operand is a scalar taken from element 0 of
the second source vector register.  A few non-commutative operations
(such as reverse subtract) subtract are encoded with special opcodes.

=== Vector-Immediate Arithmetic Instructions

A few vector arithmetic instructions have vector-immediate forms
(`.vi`) where the second scalar argument is a 5-bit signed integer
encoded in `rs2` space.

----
vadd.vi vd, vrs1, 3
----

==== Widening Vector Arithmetic Instructions

A few vector arithmetic instructions are defined to be __widening__
operations where the destination elements are stem:[2\times SEW] wide
and are stored in an even-odd vector register pair.  The first operand
can be either single or double-width. These are generally written with
a `w` suffix on the opcode.

[source]
----
Widening examples

vaddw.vv     # 2*SEW = SEW + SEW
vaddw.vs     # 2*SEW = SEW + SEW
vaddw.wv     # 2*SEW = 2*SEW + SEW
vaddw.ws     # 2*SEW = 2*SEW + SEW
----


=== Vector memory model

Vector memory instructions appear to execute in program order on the
local hart.  Vector memory instructions follow RVWMO at the
instruction level, and element operations are ordered within the
instruction as if performed by an element-ordered sequence of
syntactically independent scalar instructions.  Vector indexed-ordered
stores write elements to memory in element order.

NOTE: Other possible vector indexed store instructions include
unordered and reverse-ordered.  Vector indexed-unordered stores write
elements to memory in arbitrary order within the vector
instruction. Vector indexed reverse-ordered writes elements in reverse
element order to help with vectorized memory alias disambiguation.

==  Vector Arithmetic instructions, general formats

=== Unary operations

[source,asm]
----
   vop.s  vd, vs1        # Scalar operation
   vop.v  vd, vs1        # Unmasked vector operation
   vop.v  vd, vs1, v0.t  # Masked vector operation
   vop.v  vd, vs1, v0.f  # Complement masked vector operation
----

=== Binary register-register operations
[source,asm]
----
    vop.s   vd, vs1, vs2         # Scalar operation
    vop.vv  vd, vs1, vs2         # Vector-vector operation
    vop.vv  vd, vs1, vs2, v0.t   # Masked  vector-vector
    vop.vv  vd, vs1, vs2, v0.f
    vop.vs  vd, vs1, vs2         # Vector-scalar operation
    vop.vs  vd, vs1, vs2, v0.t   # Masked vector-scalar operation
    vop.vs  vd, vs1, vs2, v0.f   # Masked vector-scalar operation
----

=== Binary register-immediate operations
[source,asm]
----
    vop.si   vd,     vs1, imm       # Scalar operation with immediate
    vop.vi  vd,     vs1, imm       # Vector-scalar with scalar immediate.
    vop.vi  vd,     vs1, imm, v0.t
    vop.vi  vd,     vs1, imm, v0.f
----

=== Ternary register-register operations
[source,asm]
----
    vop.s    vd,  vs1, vs2, vs3  # Scalar operation
    vop.vvv  vd,  vs1, vs2, vs3  # Vector-vector-vector sources
    vop.vvv  vd,  vs1, vs2, vs3, v0.t # Masked
    vop.vvv  vd,  vs1, vs2, vs3, v0.f
    vop.vvs  vd,  vs1, vs2, vs3  # Vector-vector-scalar sources
    vop.vvs  vd,  vs1, vs2, vs3, v0.t # Masked
    vop.vvs  vd,  vs1, vs2, vs3, v0.f
----

== Vector integer arithmetic instructions

Vector integer arithmetic instructions use the currently configured
stem:[SEW] width of the source and destination vector registers.  All
vector integer arithmetic instructions can produce scalar or vector
shapes and can be masked.  A few integer instructions are
provided in a widening form, where one input is stem:[2\times
SEW]-wide and the other is stem:[SEW] wide, and the result is
stem:[2\times SEW] wide.

=== Vector Integer Add/Subtract

[source,asm]
----
    vadd.s  vd, vs1, vs2        # Scalar add
    vadd.vv vd, vs1, vs2, vm    # Vector-vector add
    vadd.vs vd, vs1, vs2, vm    # Vector-scalar add
    vadd.vi vd, vs1, imm, vm   # Vector-immediate add

    vsub.s vd, vs1, vs2          # Scalar sub
    vsub.vv vd, vs1, vs2, vm    # Vector-vector subtract
    vsub.vs vd, vs1, vs2, vm    # Vector-scalar subtract (v-s)
    vsubr.vs vd, vs1, vs2, vm   # Reverse vector-scalar subtract (s-v)
    vneg.v vd, vs1, vm         # Negate vector

    #Widening operations
    vadd2w.vv vd, vs1, vs2, vm  # vd,vs1 = 2*SEW, vs2 = SEW
    vsub2w.vv vd, vs1, vs2, vm  # vd,vs1 = 2*SEW, vs2 = SEW
----

=== Vector Shifts

Vector shifts use `log2(max(x, y))` lower bits of `vs2` for the shift value,
where `x` is the current configured element width of `vd`, and `y` is the
current configured element width of `vs1`.

[source,asm]
----
    vsl.s     vd, vs1, vs2
    vsl.vv    vd, vs1, vs2, vm
    vsl.vs    vd, vs1, vs2, vm  # vs1 << scalar
    vslr.vs    vd, vs1, vs2, vm # scalar << vs1
    vsl.vi   vd, vs1, imm, vm

    vsra.s     vd, vs1, vs2
    vsra.vv   vd, vs1, vs2, vm
    vsra.vs   vd, vs1, vs2, vm
    vsrar.vs   vd, vs1, vs2, vm
    vsra.vi  vd, vs1, imm, vm

    vsrl.s    vd, vs1, vs2
    vsrl.vv   vd, vs1, vs2, vm
    vsrl.vs   vd, vs1, vs2, vm
    vsrlr.vs   vd, vs1, vs2, vm
    vsrl.vi  vd, vs1, imm, vm
----

=== Vector Logical

[source,asm]
----
    vand.s   vd, vs1, vs2
    vand.vv  vd, vs1, vs2, vm
    vand.vs  vd, vs1, vs2, vm
    vand.vi  vd, vs1, imm, vm

    vor.s   vd, vs1, vs2
    vor.vv  vd, vs1, vs2, vm
    vor.vs  vd, vs1, vs2, vm
    vor.vi  vd, vs1, imm, vm

    vxor.s   vd, vs1, vs2
    vxor.vv  vd, vs1, vs2, vm
    vxor.vs  vd, vs1, vs2, vm
    vxor.vi  vd, vs1, imm, vm
----

=== Vector Integer Comparison

The following compare instructions write `1` to the destination register if the
comparison evaluates to true and produces `0` otherwise.
[NOTE]
`VSNE` is not needed with complementing masks but sometimes predicate results feed
into things other than predicate inputs and so `VSNE` can save an instruction.

[source,asm]
----
    vseq.vv    vd, vs1, vs2, vm
    vsne.vv    vd, vs1, vs2, vm
    vslt.vv    vd, vs1, vs2, vm
    vsltu.vv   vd, vs1, vs2, vm
    vsge.vv    vd, vs1, vs2, vm
    vsgeu.vv   vd, vs1, vs2, vm

Also need .s and .vs versions
----

These conditionals effectively `AND` in the mask when producing `0`/`1` in
output, e.g,

[source,asm]
----
    # (a < b) && (b < c) in two instructions
    vslt.vv    v0, va, vb
    vslt.vv    v0, vb, vc, vm
----

=== Vector integer multiply and divides

==== Full-width multiply/divides
These are all equivalent to scalar integer multiply/divides, and
operate on VSEW source and destination widths.

[source,asm]
----
    vmul.vv      vd, vs1, vs2, vm
    vmulh.vv     vd, vs1, vs2, vm
    vmulhsu.vv   vd, vs1, vs2, vm
    vmulhu.vv    vd, vs1, vs2, vm
    vdiv.vv      vd, vs1, vs2, vm
    vdivu.vv     vd, vs1, vs2, vm
    vrem.vv      vd, vs1, vs2, vm
    vremu.vv     vd, vs1, vs2, vm

Also need .s and .vs variants
----

==== Integer Multiply Add

The integer fused multiply-add is a destructive operation in order to save
encoding space. The two source operands `vs1`, `vs2` are multiplied
element-wise, and the result is accumulated into `vd`.

[source,asm]
----
    vmadd.vvv           vd, vs1, vs2, vm   # vd[i] += vs1[i]*vs2[i]
    vmadd.vvs           vd, vs1, vs2, vm   # vd[i] += vs1[i]*vs2[0]
    vmaddu.vvv          vd, vs1, vs2, vm
    vmaddu.vvs          vd, vs1, vs2, vm 
----

=== Integer Reduction Operations
These instructions take a vector shape as input and produce a scalar
shape.

[source,asm]
----
    vredsum.sv   vd, vs1
    vredmax.sv   vd, vs1
    vredmaxu.sv  vd, vs1
    vredmin.sv   vd, vs1
    vredminu.sv  vd, vs1
    vredand.sv   vd, vs1
    vredor.sv    vd, vs1
    vredxor.sv   vd, vs1
----

== Examples
=== Vector-vector add example

[source]
----
    # vector-vector add routine of 32-bit integers
    # void vvaddint32(size_t n, const int*x, const int*y, int*z)
    # { for (size_t i=0; i<n; i++) { z[i]=x[i]+y[i]; } }
    #
    # a0 = n, a1 = x, a2 = y, a3 = z
    # Non-vector instructions are indented
vvaddint32:
    vsetvli t0, a0, VT_VINT32 # Set vector length based on 32-bit vectors
    vlw v0, (a1)           # Get first vector
      sub a0, a0, t0         # Decrement number done
      slli t0, t0, 2         # Multiply number done by 4 bytes
      add a1, a1, t0         # Bump pointer
    vlw v1, (a2)           # Get second vector
      add a2, a2, t0         # Bump pointer
    vadd v2, v0, v1        # Sum vectors
    vsw v2, (a3)           # Store result
      add a3, a3, t0         # Bump pointer
      bnez a0, vvaddint32    # Loop back
      ret                    # Finished
----

=== Memcpy example

[source]
----
    # void *memcpy(void* dest, const void* src, size_t n)
    # a0=dest, a1=src, a2=n
    #
  memcpy:
      mv a3, a0 # Copy destination
  loop:
    vsetvli t0, a2, VT_VINT8  # Vectors of 8b
    vlb v0, (a1)              # Load bytes
      add a1, a1, t0            # Bump pointer
      sub a2, a2, t0            # Decrement count
    vsb v0, (a3)              # Store bytes
      add a3, a3, t0            # Bump pointer
      bnez a2, loop             # Any more?
      ret                       # Return
----

=== Conditional example

[source]
----
       (int16) z[i] = ((int8) x[i] < 5) ? (int16) a[i] : (int16) b[i];

Fixed 16b SEW:
loop:
    vsetvli t0, a0, VT_VINT16  # Use 16b elements.
    vlb v0, (a1)               # Get x[i], sign-extended to 16b
      sub a0, a0, t0           # Decrement element count
      add a1, a1, t0           # x[i] Bump pointer
    vslti v0, v0, 5            # Set mask in v0
      slli t0, t0, 1             # Multiply by 2 bytes
    vlh v1, (a2), v0.t         # z[i] = a[i] case
      add a2, a2, t0           # a[i] bump pointer
    vlh v1, (a3), v0.f         # z[i] = b[i] case
      add a3, a3, t0           # b[i] bump pointer
    vsh v1, (a4)               # Store z
      add a4, a4, t0           # b[i] bump pointer
      bnez a0, loop
----





== Increasing VLMAX through register grouping and `vlmul` field.

An additional field can be added to `vsetvl` configuration to increase
vector length when fewer architectural vector registers are needed by
grouping vector registers together.  The upper paired registers are
considered to add more ELEN units to the lowest-numbered vector
register.  Attempts to access the upper registers when they are
grouped raises an illegal-instruction execption.

[source]
----
 vlmul  #vregs  vnames   VLMAX
 00         32   v0-v31  VLEN/SEW
 01         16   v0-v15  2*VLEN/SEW
 10          8   v0-v7   4*VLEN/SEW
 11          4   v0-v3   8*VLEN/SEW
----

Register grouping structure:

[source]
----
vlmul
01      [v0,v16],[v1,v17],...,[v15,v31]
10      [v0,v8,v16,v24],[v1,v9,v17,v24],...,[v7,v15,v23,v31]
11      [v0,v4,v8,v12,v16,v20,v24],[v1,v5,v9,v13,v17,v21,v24],...,[v3,...,v31]
----


